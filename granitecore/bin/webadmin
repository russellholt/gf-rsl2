#!/bin/csh
#########################################################################
#              Web Server Admininstration
#------------------------------------------------------------------------
#
# Options:
#
#	-shutdown     seconds till all servers shut down
#
#	-logsystems   subsystems to log (a-g, see documentation)
#
#	-loglevel     1-6; 1 = Fatal, 2= Alert, ..., 6 = Debug
#
#	-maxusers     maximum users. Set to 0 to disallow new logins.
#
#	-create       specify output filename (default: reconfig.rsl)
#                 The filename that the server looks for is
#                 specified by the variable ReconfigFile in
#                 the _a*.rsl server config file.
#
#------------------------------------------------------------------------
#     Copyright (C) 1996, 1997 Destiny Software Corporation
##########################################################################


# reconfig output script name (override with -create option)
set fname = reconfig.rsl

#
# Set options
#

while ($#argv)
	if ($1 == "-h") then
		echo "webserver administration"
		echo "Usage: $0 [options]"
		echo "Options are:"
		echo "  -shutdown     seconds till server shut down"
		echo
		echo "  -logsystems   subsystems to log (a-g inclusive, see documentation)"
		echo
		echo "  -stat         seconds between periodic statistics logging"
		echo
		echo "  -loglevel     0-6; 0 = Emergency, 1 = Fatal, 2= Alert, ..., 6 = Debug"
		echo
		echo "  -maxusers     maximum users. Set to 0 to disallow new logins."
		echo
		echo "  -totalapps    maximun number of sessions that the server"
		echo "                   will process before shutting down."
		echo "                   Set to 0 for unlimited sessions"
		echo
		echo "  -create       specify output filename (default: reconfig.rsl)"
		echo "                   The filename that the server looks for is"
		echo "                   specified by the variable ReconfigFile in"
		echo "                   the RmgInit.rsl server config file."
#		echo
#		echo "  -p            print (and write) reconfig script file only"
#		echo "                   (do not reconfigure servers)"
		exit
	endif

	if ($1 == "-create") then
		set fname = $2
		shift; shift
	endif

	if ($1 == "-shutdown") then
		set sdt = $2
		set mu = 0
		# echo ShutdownTime = $sdt\;
		# echo MaxUsers = $mu\;
		shift; shift
	endif

	if ($1 == "-stat") then
		set sti	= $2
		shift; shift
	endif

	if ($1 == "-logsystems") then
		set lws	= $2
		# echo LogWhichSystems = \"$lws\"\;
		shift; shift
	endif

	if ($1 == "-maxusers") then
		set mu = $2
		# echo maxusers = \"$mu\"\;
		shift; shift
	endif

	if ($1 == "-loglevel") then
		set ll = $2
		# echo LogLevel = $ll\;
		shift; shift
	endif

	if ($1 == "-totalapps") then
		set ta = $2
		shift; shift
	endif

#	if ($1 == "-p") then
#		set printonly
#		shift
#	endif
end

##########################
# Write output file
##########################

if ($?sdt || $?mu || $?lws || $?ll || $?ta || $?sti) then
	if (! $?fname) then
		set fname = "reconfig.rsl"
	endif

	if (-e $fname) then			# if file exists, delete it
		rm $fname
	endif

	if ($?sti) then
		echo WebServer\#Start\.SetLogStatInterval\#reconfig\($sti\)\; >> $fname
	endif

	if ($?mu) then
		echo WebServer\#Start\.SetMaxUsers\#reconfig\($mu\)\; >> $fname
	endif

	if ($?sdt) then
		echo WebServer\#Start\.Shutdown\#reconfig\($sdt\)\; >> $fname
	endif

	if ($?lws) then
		echo WebServer\#Start\.SetLogWhichSystems\#reconfig\(\"$lws\"\)\; >> $fname
	endif

	if ($?ll) then
		echo WebServer\#Start\.SetLogLevel\#reconfig\($ll\)\; >> $fname
	endif

	if ($?ta) then
		echo WebServer\#Start\.SetTotalAppsAllowed\#reconfig\($ta\)\; >> $fname
	endif

	echo Output file $fname created. The USR1 signal must be sent
	echo to all server child processes for any changes to take effect.

	###############################################################
	# Tell all webservers to read & execute the reconfigure script.
	# (commented out until process management is back in place)
	###############################################################
	#if ($?printonly) then
	#	cat $fname
	#else	# webreconfig
	#	webreconfig
	#endif
	###############################################################

endif

echo
echo
